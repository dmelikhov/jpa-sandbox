import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.4.3' apply false
}

subprojects {
    apply plugin: 'java'

    group = 'dimas'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '11'

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    configurations.configureEach {
        resolutionStrategy.dependencySubstitution {
            def substituteVersion = { notation, a, b ->
                substitute module("$notation:$a") with module("$notation:$b")
            }
            // https://bugs.mysql.com/bug.php?id=102321
            substituteVersion('mysql:mysql-connector-java', '8.0.23', '8.0.22')
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation platform(SpringBootPlugin.BOM_COORDINATES)
        annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.mockito:mockito-core'
    }

    test {
        useJUnitPlatform()
    }
}
